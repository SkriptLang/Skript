# ExprPotionEffect
test "potion effect creation":
	# test first pattern
	set {_potion} to an ambient potion effect of speed of tier 5 without the particles whilst hiding the icon for 15 seconds
	assert {_potion} is ambient with "potion is not ambient"
	assert type of {_potion} is speed with "type is not speed"
	assert amplifier of {_potion} is 5 with "amplifier is not 5"
	assert {_potion} does not have particles with "potion has particles"
	assert {_potion} does not have an icon with "potion has an icon"
	assert duration of {_potion} is 15 seconds with "duration is not 15 seconds"
	assert {_potion} is not infinite with "potion is infinite"

	# test second pattern
	set {_potion} to an infinite ambient potion effect of speed of tier 5 without the particles whilst hiding the icon
	assert {_potion} is ambient with "potion is not ambient"
	assert type of {_potion} is speed with "type is not speed"
	assert amplifier of {_potion} is 5 with "amplifier is not 5"
	assert {_potion} does not have particles with "potion has particles"
	assert {_potion} does not have an icon with "potion has an icon"
	assert {_potion} is infinite with "potion is infinite"

	# test third pattern
	set {_potion} to an infinite ambient speed 5 without the particles whilst hiding the icon
	assert {_potion} is ambient with "potion is not ambient"
	assert type of {_potion} is speed with "type is not speed"
	assert amplifier of {_potion} is 5 with "amplifier is not 5"
	assert {_potion} does not have particles with "potion has particles"
	assert {_potion} does not have an icon with "potion has an icon"
	assert {_potion} is infinite with "potion is infinite"

	# test "matches everything" type+amplifier
	set {_potion} to speed 5
	assert type of {_potion} is speed with "type is not speed"
	assert amplifier of {_potion} is 5 with "amplifier is not 5"

# ExprPotionEffects
test "potion effects of entities/items":
	# setup
	set {_potions::1} to a potion effect of slowness 3 without the particles for 73 seconds
	set {_potions::2} to an ambient potion effect of speed of tier 5 without the particles for 15 seconds
	set {_potions::3} to speed 5
	spawn a pig at event-location:
		set {_holders::1} to event-entity
	set {_holders::2} to a potion of mundane

	loop {_potions::*}:
		loop {_holders::*}:
			add loop-value-1 to the potion effects of loop-value-2
			assert potion effects of loop-value-2 contains loop-value-1 with "holder doesn't have potion effect"
			remove loop-value-1 from the potion effects of loop-value-2
			assert potion effects of loop-value-2 doesn't contain loop-value-1 with "holder has potion effect"
			add loop-value-1 to the potion effects of loop-value-2
			assert potion effects of loop-value-2 contains loop-value-1 with "holder doesn't have potion effect"
			clear potion effects of loop-value-2
			assert potion effects of loop-value-2 are not set with "holder has potion effect"

	# cleanup
	delete the entity within {_entity}

# EffApplyPotionEffect:
test "potion effect application":
	# setup
	spawn a pig at event-location:
		set {_entity} to event-entity

	# test simple application
	apply speed to {_entity}
	assert {_entity} has the potion effect speed
	clear potion effects of {_entity}

	# test timed application
	apply slowness to {_entity} for 13 seconds
	assert {_entity} has the potion effect slowness
	assert compare_timespans(13 seconds, the duration of the first element of the potion effects of {_entity}) is true with "duration is not 13 seconds"

	# cleanup
	delete the entity within {_entity}

# ExprPotionAmplifier
test "potion amplifier property":
	# setup
	set {_potion} to a potion effect of speed
	spawn a pig at event-location:
		set {_entity} to event-entity

	# test modifying potion effect object
	set the amplifier of {_potion} to 5
	assert the amplifier of {_potion} is 5 with "amplifier is not 5"
	add 5 to the amplifier of {_potion}
	assert the amplifier of {_potion} is 10 with "amplifier is not 10"
	remove 5 from the amplifier of {_potion}
	assert the amplifier of {_potion} is 5 with "amplifier is not 5"

	# test modifying potion effect of entity
	apply speed to {_entity}
	set the amplifier of speed for {_entity} to 5
	assert the amplifier of the first element of the potion effects of {_entity} is 5 with "amplifier is not 5"
	add 5 to the amplifier of speed for {_entity}
	assert the amplifier of the first element of the potion effects of {_entity} is 10 with "amplifier is not 10"
	remove 5 from the amplifier of speed for {_entity}
	assert the amplifier of the first element of the potion effects of {_entity} is 5 with "amplifier is not 5"

	# cleanup
	delete the entity within {_entity}

# ExprPotionDuration
test "potion duration property":
	# setup
	set {_potion} to a potion effect of speed
	spawn a pig at event-location:
		set {_entity} to event-entity

	# test modifying potion effect object
	set the duration of {_potion} to 5 seconds
	assert the duration of {_potion} is 5 seconds with "duration is not 5 seconds"
	add 5 seconds to the duration of {_potion}
	assert the duration of {_potion} is 10 seconds with "duration is not 10 seconds"
	remove 5 seconds from the duration of {_potion}
	assert the duration of {_potion} is 5 seconds with "duration is not 5 seconds"

	# test modifying potion effect of entity
	apply speed to {_entity}
	set the duration of speed for {_entity} to 5 seconds
	assert compare_timespans(5 seconds, the duration of the first element of the potion effects of {_entity}) is true with "duration is not 5 seconds"
	add 5 seconds to the duration of speed for {_entity}
	assert compare_timespans(10 seconds, the duration of the first element of the potion effects of {_entity}) is true with "duration is not 10 seconds"
	remove 5 seconds from the duration of speed for {_entity}
	assert compare_timespans(5 seconds, the duration of the first element of the potion effects of {_entity}) is true with "duration is not 5 seconds"

	# cleanup
	delete the entity within {_entity}

# CondIsPotionAmbient, EffPotionAmbient
test "potion ambient property":
	# setup
	set {_potion} to a potion effect of speed
	spawn a pig at event-location:
		set {_entity} to event-entity

	# test modifying potion effect object
	make {_potion} ambient
	assert {_potion} is ambient with "potion is not ambient"
	make {_potion} not ambient
	assert {_potion} is not ambient with "potion is ambient"

	# test modifying potion effect of entity
	apply speed to {_entity}
	make speed for {_entity} ambient
	assert potion effects of {_entity} are ambient with "potion is not ambient"
	make speed for {_entity} not ambient
	assert potion effects of {_entity} are not ambient with "potion is ambient"

	# cleanup
	delete the entity within {_entity}

# CondIsPotionInfinite, EffPotionInfinite
test "potion infinite property":
	# setup
	set {_potion} to a potion effect of speed
	spawn a pig at event-location:
		set {_entity} to event-entity

	# test modifying potion effect object
	make {_potion} infinite
	assert {_potion} is infinite with "potion is not infinite"
	make {_potion} not infinite
	assert {_potion} is not infinite with "potion is infinite"

	# test modifying potion effect of entity
	apply speed to {_entity}
	make speed for {_entity} infinite
	assert potion effects of {_entity} are infinite with "potion is not infinite"
	make speed for {_entity} not infinite
	assert potion effects of {_entity} are not infinite with "potion is infinite"

	# cleanup
	delete the entity within {_entity}

# CondPotionHasIcon, EffPotionIcon
# Unfortunately, potion icons are improperly handled on older versions
# They are not properly set when converting to/from Bukkit PotionEffects
test "potion icon property" when running minecraft "1.20":
	# setup
	set {_potion} to a potion effect of speed
	spawn a pig at event-location:
		set {_entity} to event-entity

	# test modifying potion effect object
	show the icon for {_potion}
	assert {_potion} has an icon with "potion does not have an icon"
	hide the icon for {_potion}
	assert {_potion} does not have an icon with "potion has an icon"

	# test modifying potion effect of entity
	apply speed to {_entity}
	show the icon for speed for {_entity}
	assert potion effects of {_entity} have an icon with "potion does not have an icon"
	hide the icon for speed for {_entity}
	assert potion effects of {_entity} do not have an icon with "potion has an icon"

	# cleanup
	delete the entity within {_entity}

# CondPotionHasParticles, EffPotionParticles
test "potion particles property":
	# setup
	set {_potion} to a potion effect of speed
	spawn a pig at event-location:
		set {_entity} to event-entity

	# test modifying potion effect object
	show the particles for {_potion}
	assert {_potion} has particles with "potion does not have particles"
	hide the particles for {_potion}
	assert {_potion} does not have particles with "potion has particles"

	# test modifying potion effect of entity
	apply speed to {_entity}
	show particles for speed for {_entity}
	assert potion effects of {_entity} have particles with "potion does not have particles"
	hide particles for speed for {_entity}
	assert potion effects of {_entity} do not have particles with "potion has particles"

	# cleanup
	delete the entity within {_entity}

# CondHasPotion, CondIsPoisoned, EffPoison
test "poison":
	# setup
	spawn a pig at event-location:
		set {_entity} to event-entity

	# test simple poisoning
	assert {_entity} is not poisoned with "entity is poisoned"
	assert {_entity} does not have the potion effect poison with "entity is poisoned"
	poison {_entity}
	assert {_entity} has the potion effect poison with "entity is not poisoned"
	assert {_entity} is poisoned with "entity is not poisoned"

	# test curing
	cure {_entity} of poison
	assert {_entity} is not poisoned with "entity is poisoned"

	# test custom duration poisoning
	poison {_entity} for 15 seconds
	set {_poison} to the first element of the potion effects of {_entity}
	assert type of {_poison} is poison with "entity is not poisoned"
	assert compare_timespans(15 seconds, duration of {_poison}) is true with "poison duration is not 15 seconds"

	# cleanup
	delete the entity within {_entity}

on entity potion effect modification:
	event-potioneffecttype is glowing
	event-potioneffectcause is plugin
	event-potioneffect does not have particles
	duration of event-potioneffect is 17 seconds
	set {potion_event::1} to true

on entity potion effect modification of glowing due to plugin:
	event-potioneffect does not have particles
	duration of event-potioneffect is 17 seconds
	set {potion_event::2} to true

test "potion application event":
	# setup
	spawn a pig at event-location:
		set {_entity} to event-entity

	apply potion effect of glowing without particles for 17 seconds to {_event-entity}
	loop {potion_event::*}:
		assert loop-value is true with "potion event %loop-index% failed"

	# cleanup
	delete the entity within {_entity}
	delete {potion_event::*}

test "CondIsPotionInstant":
	assert instant health is instant with "instant health is not instant"
	assert speed is not instant with "speed is instant"

test "ExprPotionEffectTypeCategory" when running minecraft "1.21":
	assert category of instant health is beneficial with "instant health is not beneficial"
	assert weakness is harmful with "weakness is not harmful"
	assert potion effect type category of glowing is neutral with "glowing is not neutral"

#
# Regression Tests
#

test "potion missing base type": #6756
	assert potion effects of (plain potion of mundane) is not set with "it should not have any effects"

#
# Utilities
#

local function compare_timespans(expected: timespan, found: timespan) returns boolean:
	return whether difference between {_expected} and {_found} is less than or equal to 0.5 seconds
