using tool components

test "tool components - new" when running minecraft "1.20.6":
	set {_component} to a blank tool component
	set the default mining speed of {_component} to 20
	assert the default mining speed of {_component} is 20 with "Default mining speed of component was not set"
	set the damage per block of {_component} to 10
	assert the damage per block of {_component} is 10 with "Damage per block of component was not set"

	set {_rule} to a custom tool rule with block types oak log, stone and obsidian
	set the tool rule speed of {_rule} to 5
	add {_rule} to the tool rules of {_component}
	assert the tool rules of {_component} contains {_rule} with "Tool rule was not added to component"

test "tool components - itemtype" when running minecraft "1.20.6":
	set {_item} to an iron pickaxe (item type)
	set the default mining speed of {_item} to 20
	assert the default mining speed of {_item} is 20 with "Default mining speed of itemtype was not set"
	set the damage per block of {_item} to 10
	assert the damage per block of {_item} is 10 with "Damage per block of itemtype was not set"

	set {_rule} to a custom tool rule with block types oak log, stone and obsidian
	set the tool rule speed of {_rule} to 5
	add {_rule} to the tool rules of {_item}
	assert the tool rules of {_item} contains {_rule} with "Tool rule was not added to itemtype"

test "tool components - itemstack" when running minecraft "1.20.6":
	set {_item} to an iron pickaxe (item stack)
	set the default mining speed of {_item} to 20
	assert the default mining speed of {_item} is 20 with "Default mining speed of itemstack was not set"
	set the damage per block of {_item} to 10
	assert the damage per block of {_item} is 10 with "Damage per block of itemstack was not set"

	set {_rule} to a custom tool rule with block types oak log, stone and obsidian
	set the tool rule speed of {_rule} to 5
	add {_rule} to the tool rules of {_item}
	assert the tool rules of {_item} contains {_rule} with "Tool rule was not added to itemstack"

test "tool components - slot" when running minecraft "1.20.6":
	set {_gui} to a chest inventory with 1 row
	set slot 1 of {_gui} to a netherite pickaxe

	set the default mining speed of (slot 1 of {_gui}) to 20
	assert the default mining speed of (slot 1 of {_gui}) is 20 with "Default mining speed of slot was not set"
	set the damage per block of (slot 1 of {_gui}) to 10
	assert the damage per block of (slot 1 of {_gui}) is 10 with "Damage per block of slot was not set"

	set {_rule} to a custom tool rule with block types oak log, stone and obsidian
	set the tool rule speed of {_rule} to 5
	add {_rule} to the tool rules of (slot 1 of {_gui})
	assert the tool rules of (slot 1 of {_gui}) contains {_rule} with "Tool rule was not added to slot"

test "tool components - copy" when running minecraft "1.20.6":
	set {_item} to a golden pickaxe
	set {_component} to a copy of (tool component of {_item})
	set the default mining speed of {_component} to 20
	assert the default mining speed of {_component} is 20 with "Default mining speed of copy was not set"
	set the damage per block of {_component} to 10
	assert the damage per block of {_component} is 10 with "Damage per block of copy was not set"

	set {_rule} to a custom tool rule with block types oak log, stone and obsidian
	set the tool rule speed of {_rule} to 5
	add {_rule} to the tool rules of {_component}
	assert the tool rules of {_component} contains {_rule} with "Tool rule was not added to copy"

test "tool components - tool rule" when running minecraft "1.20.6":
	set {_rule} to a custom tool rule with block types stone and cobblestone
	set the tool rule block types of {_rule} to diamond ore and gold ore
	assert the tool rule block types of {_rule} is a diamond ore and a gold ore with "Block types of tool rule did not change"
	set the tool rule speed of {_rule} to 50
	assert the tool rule speed of {_rule} is 50 with "Tool rule speed did not change"
	enable the tool rule drops for {_rule}
	assert the tool rule drops of {_rule} is enabled with "Tool rule drops should be enabled"
	disable the tool rule drops for {_rule}
	assert the tool rule drops of {_rule} is disabled with "Tool rule drops should be disabled"

	set {_component1} to the tool component of a leather helmet
	set {_component2} to the tool component of an iron helmet

	clear the tool rules of {_component1}
	clear the tool rules of {_component2}

	add {_rule} to the tool rules of {_component1}
	add {_rule} to the tool rules of {_component2}
	assert the tool rules of {_component1} contains {_rule} with "Tool rule was not added to component1"
	assert the tool rules of {_component2} contains {_rule} with "Tool rule was not added to component2"
	# NOT DONE