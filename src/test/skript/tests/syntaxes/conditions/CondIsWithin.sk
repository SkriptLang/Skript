test "within condition":
	# two locations
	set {_loc1} to location(0, 0, 0, "world")
	set {_loc2} to location(20, 20, 20, "world")
	assert location(10, 10, 10, "world") is within {_loc1} and {_loc2} with "failed within two locs"
	assert location(10, -10, 10, "world") is not within {_loc1} and {_loc2} with "failed within two locs"
	assert location(0, 0, 0, "world") is not within {_none} and {_none} with "failed within two locs"

	# chunks
	set {_chunk1} to chunk at {_loc1}
	assert location(10, 10, 10, "world") is within {_chunk1} with "failed within chunk"
	assert location(-10, 10, -10, "world") is not within {_chunk1} with "failed within chunk"

	# worlds
	assert location(10, 10, 10, "world") is within world("world") with "failed within world"

	# blocks
	set block at {_loc1} to stone
	assert {_loc1} is within block at {_loc1} with "failed within block"
	assert {_loc2} is not within block at {_loc1} with "failed within block"
	# special case, non-full blocks
	set block at {_loc1} to lime carpet
	assert {_loc1} is within block at {_loc1} with "failed within block"
	assert ({_loc1} ~ vector(0,0.3,0)) is not within block at {_loc1} with "failed within block"

	# entities
	set {_loc} to test-location
	spawn a pig at {_loc}
	set {_pig} to last spawned entity
	assert {_loc} is within {_pig} with "failed within entity"
	assert {_loc1} is not within {_pig} with "failed within entity"

	delete random entity of {_pig}

test "within multiple condition":
	set {_chunk} to chunk at test-location
	spawn a pig at test-location:
		set {_entity} to entity
	assert {_entity} is within test-world and {_chunk} with "Failed within world and chunk"
	assert {_entity} is within test-world or {_chunk} with "Failed within world or chunk"
	clear entity within {_entity}

	set {_loc1} to location of test-block
	set {_loc2} to {_loc1} ~ vector(1, 0, 0)
	set {_old1} to block data of block at {_loc1}
	set {_old2} to block data of block at {_loc2}
	set blocks at {_loc1} and {_loc2} to stone
	set {_check1} to {_loc1} ~ vector(0.2, 0, 0)
	set {_check2} to {_loc1} ~ vector(0.8, 0, 0)
	assert {_check1} is within block at {_loc1} or block at {_loc2} with "Failed within block or block"
	assert {_check2} is within block at {_loc1} or block at {_loc2} with "Failed within block or block"

	set block data of block at {_loc1} to {_old1}
	set block data of block at {_loc2} to {_old2}
