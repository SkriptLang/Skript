test "in love valid entity":
	delete all cows
	set {_spawn} to spawn of world "world"
	spawn adult cow at {_spawn}
	set {_cow} to last spawned cow
	assert love ticks of {_cow} is 0 seconds with "Cow love ticks was not 0 seconds after spawning"
	assert {_cow} is not in love with "Cow is in love after spawning"
	set love ticks of {_cow} to 10 seconds
	assert {_cow} is in love with "Cow is not in love after setting love ticks"
	add 10 seconds to love ticks of {_cow}
	assert love ticks of {_cow} > 10 seconds with "Cow love ticks didn't go up after adding"
	remove 1 minute from love ticks of {_cow}
	assert {_cow} is not in love with "Cow is in love after removing more love than possible"
	add 10 minutes to love ticks of {_cow}
	assert {_cow} is in love with "Cow didn't enter love mode after adding love ticks"
	reset love ticks of {_cow}
	assert {_cow} is not in love with "Cow was still in love even after resetting love ticks"
	delete all cows

test "in love invalid entity":
	delete all zombies
	set {_spawn} to spawn of world "world"
	spawn adult zombie at {_spawn}
	set {_zombie} to last spawned zombie
	assert love ticks of {_zombie} is 0 seconds with "Zombie was in love Test1"
	set love ticks of {_zombie} to 10 minutes
	assert love ticks of {_zombie} is 0 seconds with "Zombie was in love Test2"
	delete all zombies
