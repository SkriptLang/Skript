test "in love valid entity (timespan)":
	delete all cows
	set {_spawn} to spawn of world "world"
	spawn adult cow at {_spawn}
	set {_cow} to last spawned cow
	assert love ticks of {_cow} is 0 seconds with "Cow love ticks was not 0 seconds after spawning"
	assert {_cow} is not in love with "Cow is in love after spawning"
	set love ticks of {_cow} to 10 seconds
	assert {_cow} is in love with "Cow is not in love after setting love ticks"
	delete all cows

test "in love valid entity (ticks)":
	delete all cows
	set {_spawn} to spawn of world "world"
	spawn adult cow at {_spawn}
	set {_cow} to last spawned cow
	assert love ticks of {_cow} is 0 seconds with "Cow love ticks was not 0 seconds after spawning"
	assert {_cow} is not in love with "Cow is in love after spawning"
	set love ticks of {_cow} to 15000
	assert {_cow} is in love with "Cow is not in love after setting love ticks"
	assert love ticks of {_cow} > 10 minutes with "Cow love ticks were below 10 minutes, tick conversion failed"
	reset love ticks of {_cow}
	assert {_cow} is not in love with "Cow was still in love after resetting ticks"
	add 10 seconds and 300 to love ticks of {_cow}
	assert {_cow} is in love with "Cow wasn't in love adding using add changer"
	delete all cows

test "in love invalid entity":
	delete all zombies
	set {_spawn} to spawn of world "world"
	spawn adult zombie at {_spawn}
	set {_zombie} to last spawned zombie
	assert love ticks of {_zombie} is 0 seconds with "Zombie was in love Test1"
	set love ticks of {_zombie} to 10 minutes
	assert love ticks of {_zombie} is 0 seconds with "Zombie was in love Test2"
	delete all zombies