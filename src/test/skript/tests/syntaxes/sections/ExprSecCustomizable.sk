
test "customizable chest inventory type":
	set {_gui} to a custom chest inventory:
		set slot 0 to a diamond
	assert {_gui} is set with "Inventory should not be null"
	assert {_gui} is a chest inventory with "Inventory should be a chest inventory"
	assert slot 0 of {_gui} is a diamond with "Slot 0 of inventory should be a diamond"

test "customizable inventory":
	set {_gui} to a custom chest inventory named "TEST-INVENTORY" with 6 rows:
		set slot 0 to a diamond
	assert {_gui} is set with "Inventory should not be null"
	assert {_gui} is a chest inventory with "Inventory should be a chest inventory"
	assert slot 0 of {_gui} is a diamond with "Slot 0 of inventory should be a diamond"

test "customizable inventory type as inventory":
	set {_gui} to a custom inventory of hopper inventory:
		set slot 0 to a diamond
	assert {_gui} is set with "Inventory should not be null"
	assert {_gui} is a hopper inventory with "Inventory should be a hopper inventory"
	assert slot 0 of {_gui} is a diamond with "Slot 0 of inventory should be a diamond"

test "customizable item type":
	set {_item} to a custom diamond:
		set name to "TESTING-NAME"
		set lore to "TESTING-LORE"
	assert {_item} is set with "Item should not be null"
	assert name of {_item} is "TESTING-NAME" with "Name of item was not set"
	assert lore of {_item} is "TESTING-LORE" with "Lore of item was not set"

test "stacked customizable":
	set {_gui} to a custom chest inventory:
		set slot 0 to a custom diamond:
			set name to "TESTING-NAME-STACKED"
			set lore to "TESTING-LORE-STACKED"
	assert {_gui} is set with "Inventory should not be null"
	assert {_gui} is a chest inventory with "Inventory should be a chest inventory"
	assert slot 0 of {_gui} is a diamond with "Slot 0 of inventory should be a diamond"
	assert name of slot 0 of {_gui} is "TESTING-NAME-STACKED" with "Name of item was not set"
	assert lore of slot 0 of {_gui} is "TESTING-LORE-STACKED" with "Lore of item was not set"

test "stacked customizable - section value":
	set {_gui} to a custom chest inventory:
		set slot 0 of section-value to a custom diamond:
			set name of section-value to "TESTING-NAME-STACKED"
			set lore of section-value to "TESTING-LORE-STACKED"
	assert {_gui} is set with "Inventory should not be null"
	assert {_gui} is a chest inventory with "Inventory should be a chest inventory"
	assert slot 0 of {_gui} is a diamond with "Slot 0 of inventory should be a diamond"
	assert name of slot 0 of {_gui} is "TESTING-NAME-STACKED" with "Name of item was not set"
	assert lore of slot 0 of {_gui} is "TESTING-LORE-STACKED" with "Lore of item was not set"

test "stacked customizable - section typed":
	set {_gui} to a custom chest inventory:
		set slot 0 of section-inventory to a custom diamond:
			set name of section-itemtype to "TESTING-NAME-STACKED"
			set lore of section-itemtype to "TESTING-LORE-STACKED"
	assert {_gui} is set with "Inventory should not be null"
	assert {_gui} is a chest inventory with "Inventory should be a chest inventory"
	assert slot 0 of {_gui} is a diamond with "Slot 0 of inventory should be a diamond"
	assert name of slot 0 of {_gui} is "TESTING-NAME-STACKED" with "Name of item was not set"
	assert lore of slot 0 of {_gui} is "TESTING-LORE-STACKED" with "Lore of item was not set"

test "customizable parse errors" when running minecraft "1.20.0":
	parse:
		set {_test} to a custom fall:
			broadcast "filler"
	assert last parse logs is "The provided object can reference multiple types. Which none can be customized." with "Parse error was not printed"

	parse:
		set {_test} to a custom fall (damage type):
			broadcast "filler"
	assert last parse logs is "A damage type cannot be customized." with "Parse error was not printed"

	parse:
		set {_test} to a custom damage type of fall:
			broadcast "filler"
	assert last parse logs is "A damage type cannot be customized." with "Parse error was not printed"

	parse:
		set {_test} to a custom inventory of fall:
			broadcast "filler"
	assert last parse logs is "The provided expression 'fall' cannot be customized as an inventory." with "Parse error was not printed"

	parse:
		set {_test} to a custom inventory type of chest inventory:
			broadcast "filler"
	assert last parse logs is "An inventory type cannot be customized. But can be customized as an inventory." with "Parse error was not printed"

test "customizable disallowed":
	parse:
		set {_test} to a custom last spawned entity:
			broadcast "filler"
	assert last parse logs is "An entity cannot be customized." with "Parse error was not printed"

	parse:
		set {_test} to a custom cow:
			broadcast "filler"
	assert last parse logs is "An entity type cannot be customized." with "Parse error was not printed"

	parse:
		set {_test} to a custom (2 cows):
			broadcast "filler"
	assert last parse logs is "An entity type cannot be customized." with "Parse error was not printed"
