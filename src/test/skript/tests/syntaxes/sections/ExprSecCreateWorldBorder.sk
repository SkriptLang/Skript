test "create worldborder":
	set {_border} to a worldborder
	assert {_border} is set with "Failed to create worldborder without a section"

	set {_border2} to a worldborder:
		set worldborder center of event-worldborder to location(1, 0, 2)
		set worldborder damage amount of event-worldborder to 23
		set worldborder damage buffer of event-worldborder to 10
		set worldborder size of event-worldborder to 30
		set worldborder warning distance of event-worldborder to 54
		set worldborder warning time of event-worldborder to 10 seconds
	set {_center2::x} to x coordinate of worldborder center of {_border2}
	set {_center2::z} to z coordinate of worldborder center of {_border2}
	assert {_center2::*} is 1 and 2 with "Worldborder center did not set properly in section"
	assert worldborder damage amount of {_border2} is 23 with "Worldborder damage amount did not set properly in section"
	assert worldborder damage buffer of {_border2} is 10 with "Worldborder damage buffer did not set properly in section"
	assert worldborder size of {_border2} is 30 with "Worldborder size did not set properly in section"
	assert worldborder warning distance of {_border2} is 54 with "Worldborder warning distance did not set properly in section"
	assert worldborder warning time of {_border2} is 10 seconds with "Worldborder warning time did not set properly in section"

	set {_border3} to a worldborder:
		set worldborder center to location(-1, 0, 5)
		set worldborder damage amount to 5
		set worldborder damage buffer to 20
		set worldborder size to 15
		set worldborder warning distance to 25
		set worldborder warning time to 15 seconds
	set {_center3::x} to x coordinate of worldborder center of {_border3}
	set {_center3::z} to z coordinate of worldborder center of {_border3}
	assert {_center3::*} is -1 and 5 with "Worldborder center did not set properly in section (with worldborder omitted)"
	assert worldborder damage amount of {_border3} is 5 with "Worldborder damage amount did not set properly in section (with worldborder omitted)"
	assert worldborder damage buffer of {_border3} is 20 with "Worldborder damage buffer did not set properly in section (with worldborder omitted)"
	assert worldborder size of {_border3} is 15 with "Worldborder size did not set properly in section (with worldborder omitted)"
	assert worldborder warning distance of {_border3} is 25 with "Worldborder warning distance did not set properly in section (with worldborder omitted)"
	assert worldborder warning time of {_border3} is 15 seconds with "Worldborder warning time did not set properly in section (with worldborder omitted)"
