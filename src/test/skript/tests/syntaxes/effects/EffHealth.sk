test "health effect":
	clear all entities
	set {_i} to diamond sword
	assert {_i}'s damage value is 0 with "new item durability failed"
	damage {_i} by 50
	assert {_i}'s damage value is 50 with "damage item failed"
	repair {_i} by 49
	assert {_i}'s damage value is 1 with "repair item failed"

	spawn cow at location(0, 64, 0, world "world")
	set {_m} to last spawned cow
	assert health of {_m} is 5 with "default cow health failed"
	damage {_m} by 0.5
	assert health of {_m} is 4.5 with "damage cow ##1 failed"
	damage {_m} by 99
	assert health of {_m} is 0 with "damage cow ##2 failed"
	heal {_m} by 1
	assert health of {_m} is 1 with "heal cow ##1 failed"
	heal {_m} by 0.5
	assert health of {_m} is 1.5 with "heal cow ##2 failed"
	heal {_m} by 99
	assert health of {_m} is 5 with "heal cow ##3 failed"
	clear all entities

	set {_item1} and {_item1Copy} to diamond sword with damage 100
	set {_item2} and {_item2Copy} to iron sword
	repair {_item2} and {_item1}
	assert {_item1} is iron sword to fail with "{_item1} is an iron sword even though it was diamond before repair"
	assert {_item2} is {_item2Copy} with "{_item2} was no longer the same item"