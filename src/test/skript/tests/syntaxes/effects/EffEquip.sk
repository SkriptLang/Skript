test "equip effect":

	# === ARMOUR ===

	spawn a zombie at (spawn of world "world"):
		set {_z} to entity

	clear helmet of {_z}
	clear chestplate of {_z}
	clear leggings of {_z}
	clear boots of {_z}

	assert helmet of {_z} is air with "the modified zombie shouldn't be wearing a helmet"
	assert chestplate of {_z} is air with "the modified zombie shouldn't be wearing a chestplate"
	assert leggings of {_z} is air with "the modified zombie shouldn't be wearing leggings"
	assert boots of {_z} is air with "the modified zombie shouldn't be wearing boots"

	equip {_z} with iron helmet
	equip {_z} with diamond chestplate
	equip {_z} with diamond leggings
	equip {_z} with any boots
	assert helmet of {_z} is iron helmet with "equipping a zombie with a helmet should do exactly that"
	assert chestplate of {_z} is diamond chestplate with "equipping a zombie with a chestplate should do exactly that"
	assert leggings of {_z} is diamond leggings with "equipping a zombie with leggings should do exactly that"
	assert boots of {_z} is any boots with "equipping a zombie with boots should do exactly that"

	unequip {_z}'s armour
	assert helmet of {_z} is air with "unequipping an entity should remove its armour"
	assert chestplate of {_z} is air with "unequipping an entity should remove its armour"
	assert leggings of {_z} is air with "unequipping an entity should remove its armour"
	assert boots of {_z} is air with "unequipping an entity should remove its armour"

	# === SADDLES ===

	spawn a horse at (spawn of world "world"):
		set {_h} to entity
	spawn a pig at (spawn of world "world"):
		set {_p} to entity

	assert {_h} and {_p} don't have saddles with "normally spawned horses and pigs should not have saddles"

	equip {_h} and {_p} with saddle
	assert {_h} and {_p} have saddles with "equipping a horse or pig with a saddle should do exactly that"

	unequip saddle from {_h} and {_p}
	assert {_h} and {_p} don't have saddles with "unequipping a saddle from a horse or pig should do exactly that"

	# === WOLF ARMOUR ===

	parse if running minecraft "1.20.5":
		spawn a wolf at (spawn of world "world"):
			set {_w} to entity

		equip {_w} with wolf armor
		assert {_w} is wearing wolf armor with "equipping a wolf with wolf armor should do exactly that"

		unequip wolf armor from {_w}
		assert {_w} is not wearing wolf armor with "unequipping wolf armor from a wolf should do exactly that"

	delete entity within {_z}
	delete entity within {_h}
	delete entity within {_p}
	delete entity within {_w}
