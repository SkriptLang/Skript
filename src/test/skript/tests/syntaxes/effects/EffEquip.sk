test "equip effect":
	spawn a zombie at spawn of "world":
		set {_entity} to event-entity

	equip {_entity} with a diamond chestplate
	assert chestplate of {_entity} is a diamond chestplate with "entity was not wearing a diamond chestplate"
	set chestplate of {_entity} to air

	equip {_entity} with a diamond chestplate named "Test"
	assert chestplate of {_entity} is a diamond chestplate named "Test" with "entity was not wearing a named diamond chestplate"

	unequip diamond chestplate of {_entity}
	assert chestplate of {_entity} is air with "entity was not wearing air"

	equip {_entity} with a turtle helmet named "test"
	assert helmet of {_entity} is a turtle helmet named "test" with "entity was not wearing a named turtle helmet"

	make {_entity} wear a dirt block as a hat
	assert helmet slot of {_entity} is a dirt block with "entity was not wearing a dirt block as a hat"

	unequip helmet of {_entity}
	assert helmet of {_entity} is air with "entity was not wearing air"

	equip {_entity} with a diamond chestplate
	equip {_entity} with a diamond helmet
	equip {_entity} with a diamond leggings
	equip {_entity} with diamond boots

	assert chestplate of {_entity} is a diamond chestplate with "entity is not wearing a diamond chestplate"
	assert helmet of {_entity} is a diamond helmet with "entity is not wearing a diamond helmet"
	assert leggings of {_entity} is a diamond leggings with "entity is not wearing diamond leggings"
	assert boots of {_entity} is diamond boots with "entity is not wearing diamond boots"

	unequip all armor of {_entity}

	assert chestplate of {_entity} is air with "entity is not wearing air"
	assert helmet of {_entity} is air with "entity is not wearing air"
	assert leggings of {_entity} is air with "entity is not wearing air"
	assert boots of {_entity} is air with "entity is not wearing air"

	delete the entity within {_entity}
