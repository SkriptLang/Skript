test "wax copper golem" when running minecraft "1.21.9":
	spawn a copper golem at test-location:
		set {_entity} to entity

	assert {_entity} is not waxed with "Copper golem should not be waxed after spawn"
	wax {_entity}
	assert {_entity} is waxed with "Copper golem should be waxed"
	unwax {_entity}
	assert {_entity} is not waxed with "Copper golem should be unwaxed"

	clear entity within {_entity}

test "wax copper blocks":
	set {_old} to the block data of test-block

	set {_waxTag} to paper tag "waxed_copper_blocks"
	set {_unwaxTag} to paper tag "unwaxed_copper_blocks"
	set {_blocks::*} to the tag contents of {_waxTag}
	loop {_blocks::*}:
		set test-block to loop-value
		unwax test-block
		assert test-block is tagged with {_unwaxTag} with "%loop-value% should be unwaxed, but was %type of test-block%"
		wax test-block
		assert test-block is tagged with {_waxTag} with "%loop-value% should be waxed, but was %type of test-block%"

	set the block data of test-block to {_old}

test "wax copper chest" when running minecraft "1.21.9":
	set {_old} to the block data of test-block

	set test-block to a copper chest
	set slot 1 of inventory of test-block to a diamond
	assert slot 1 of inventory of test-block is a diamond with "Diamond was not initially set into inventory"
	wax test-block
	assert slot 1 of inventory of test-block is a diamond with "Inventory did not retain items when being waxed"
	unwax test-block
	assert slot 1 of inventory of test-block is a diamond with "Inventory did not retain items when being unwaxed"

	set the block data of test-block to {_old}
