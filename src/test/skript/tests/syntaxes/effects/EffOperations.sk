test "operations numbers":
	set {_num} to 1
	multiply {_num} by 15
	assert {_num} is 15 with "Failed multiplication for single number"
	divide {_num} by 5
	assert {_num} is 3 with "Failed division for single number"
	raise {_num} to the power of 2
	assert {_num} is 9 with "Failed exponentiation for single number"

	set {_nums::*} to 2, 3, 4 and 5
	multiply {_nums::*} by 10
	assert {_nums::*} is (20, 30, 40 and 50) with "Failed multiplication for number list"
	divide {_nums::*} by 5
	assert {_nums::*} is (4, 6, 8 and 10) with "Failed division for number list"
	raise {_nums::*} to the power of 2
	assert {_nums::*} is (16, 36, 64 and 100) with "Failed exponentiation for number list"

test "operations vectors by vectors":
	set {_vector} to vector(1,1,1)
	multiply {_vector} by vector(4,6,8)
	assert {_vector} is vector(4,6,8) with "Failed multiplication for single vector by vector"
	divide {_vector} by vector(2,3,4)
	assert {_vector} is vector(2,2,2) with "Failed division for single vector by vector"

	set {_vectors::*} to vector(2,2,2), vector(3,3,3) and vector(4,4,4)
	multiply {_vectors::*} by vector(2,5,10)
	assert {_vectors::*} is (vector(4,10,20), vector(6,15,30) and vector(8,20,40)) with "Failed multiplication for vector list by vector"
	divide {_vectors::*} by vector(1,5,5)
	assert {_vectors::*} is (vector(4,2,4), vector(6,3,6) and vector(8,4,8))  with "Failed division for vector list by vector"

test "operations vectors by numbers":
	set {_vector} to vector(1,1,1)
	multiply {_vector} by 10
	assert {_vector} is vector(10,10,10) with "Failed multiplication for single vector by number"
	divide {_vector} by 2
	assert {_vector} is vector(5,5,5) with "Failed division for single vector by number"

	set {_vectors::*} to vector(2,2,2), vector(3,3,3) and vector(4,4,4)
	multiply {_vectors::*} by 4
	assert {_vectors::*} is (vector(8,8,8), vector(12,12,12) and vector(16,16,16)) with "Failed multiplication for vector list by number"
	divide {_vectors::*} by 2
	assert {_vectors::*} is (vector(4,4,4), vector(6,6,6) and vector(8,8,8)) with "Failed division for vector list by number"

test "operations timespans by numbers":
	set {_timespan} to 1 hour
	multiply {_timespan} by 4
	assert {_timespan} is 4 hours with "Failed multiplication for single timespan by number"
	divide {_timespan} by 2
	assert {_timespan} is 2 hours with "Failed division for single timespan by number"

	set {_timespans::*} to (1 minute), (5 minutes) and (10 minutes)
	multiply {_timespans::*} by 5
	assert {_timespans::*} is ((5 minutes), (25 minutes) and (50 minutes)) with "Failed multiplication for timespan list by number"
	divide {_timespans::*} by 2
	assert {_timespans::*} is ((2 minutes and 30 seconds), (12 minutes and 30 seconds) and (25 minutes)) with "Failed division for timespan list by number"

test "operations error":
	parse:
		multiply 1 by 2
	assert last parse logs is set with "Operations should only work for variables - multiplication"

	parse:
		divide 20 by 4
	assert last parse logs is set with "Operations should only work for variables - division"

	parse:
		raise 3 to the powwer of 100
	assert last parse logs is set with "Operations should only work for variables - exponentiation"
