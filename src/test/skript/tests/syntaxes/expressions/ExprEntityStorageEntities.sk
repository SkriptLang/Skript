test "entity block storage entities" when running minecraft "1.21":
	###
		Combines:
			ExprEntityStorageEntities
			EffReleaseEntityStorage
			CondEntityStorageIsFull
	###

	clear all bees

	set {_old} to block at test-block
	set block at test-block to beehive
	set {_hive} to block at test-block

	set the maximum stored entities entities of {_hive} to 20
	assert the maximum stored entities of {_hive} is 20 with "Maximum stored entities was not changed"
	spawn 10 bees at test-location:
		add entity to {_bees::*}
	add {_bees::*} to the stored entities of {_hive}
	assert the stored entities of {_hive} is 10 with "Stored entities did not have bees added"
	assert the stored entities of {_hive} is not full with "Stored entities should not be full"

	spawn 10 bees at test-location:
		add entity to {_bees2::*}
	add {_bees2::*} to the stored entities of {_hive}
	assert the stored entities of {_hive} is 20 with "Stored entities did not have additional bees added"
	assert the stored entities of {_hive} is full with "Stored entities should be full"

	release the stored entities of {_hive}
	assert the stored entities of {_hive} is 0 with "Stored entities should be empty after releasing"

	clear all bees
	set block at test-block to {_old}
