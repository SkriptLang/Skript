test "entity block storage entities" when running minecraft "1.21":
	###
		Combines:
			ExprEntityStorageEntities
			ExprEntityStorageMaxEntities
			EffReleaseEntityStorage
			CondEntityStorageIsFull
	###

	clear all bees

	set {_old} to block at test-block
	set block at test-block to beehive
	set {_hive} to block at test-block

	set the entity block storage max entities of {_hive} to 20
	assert the entity block storage max entities of {_hive} is 20 with "Entity block storage max entities was not changed"
	spawn 10 bees at test-location:
		add entity to {_bees::*}
	add {_bees::*} to the entity block storage stored entities of {_hive}
	assert the entity block storage stored entities of {_hive} is 10 with "Entity block storage did not have bees added"
	assert the entity block storage of {_hive} is not full with "Entity block storage should not be full"

	spawn 10 bees at test-location:
		add entity to {_bees2::*}
	add {_bees2::*} to the entity block storage stored entities of {_hive}
	assert the entity block storage stored entities of {_hive} is 20 with "Entity block storage did not have additional bees added"
	assert the entity block storage of {_hive} is full with "Entity block storage should be full"

	release the entity block storage stored entities of {_hive}
	assert the entity block storage stored entities of {_hive} is 0 with "Entity block storage should be empty after releasing"

	clear all bees
	set block at test-block to {_old}
