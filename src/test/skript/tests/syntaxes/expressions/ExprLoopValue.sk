test "previous+next loop value":
	loop 5 times:
		if any:
			{_last} is set
			previous loop-number is set
		then:
			assert {_last} = previous loop-number with "previous loop number does not match"
		if {_next} is set:
			assert loop-number = {_next} with "current loop number does not match the expected next loop number"
		set {_last} to loop-number
		set {_next} to next loop-number

	clear {_last}
	clear {_next}

	set {_list::*} to iron ingot, gold ingot, diamond, emerald and netherite ingot
	loop {_list::*}:
		if any:
			{_last} is set
			previous loop-value is set
		then:
			assert {_last} = previous loop-value with "previous loop value does not match"
		if {_next} is set:
			assert loop-value = {_next} with "current loop value does not match the expected next loop value"
		set {_last} to loop-value
		set {_next} to next loop-value
