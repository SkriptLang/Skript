test "potion effects":
	spawn a sheep at test-location:
		set {_e} to entity

	set {_p} to potion effect of speed 1 without particles for 10 minutes
	add {_p} to potion effects of {_e}

	assert type of potion effects of {_e} contains speed with "the sheep should have had the speed potion type"

	set {_i} to potion of speed
	assert type of potion effects of {_i} contains speed with "the item should have had the speed potion type"

	set {_i} to potion of slowness
	assert type of potion effects of {_i} contains slowness with "the item should have had the slowness potion type"

	set {_pe::1} to infinite potion effect of darkness without particles without the icon
	set {_pe::2} to infinite ambient potion effect of night vision of tier 2 without particles
	set {_pe::3} to potion effect of nausea for 10 seconds
	set {_pe::4} to ambient potion effect of slowness without particles without the icon for 2 minutes

	assert size of {_pe::*} = 4 with "There should have been 4 potion effects created"
	assert "%{_pe::1}%" contains "without particles for infinity without icon" with "The potion effect should be infinite without particles or an icon"
	assert "%{_pe::2}%" contains "ambient" and "without particles for infinity" with "The potion effect should be ambient, without particles and for infinity"
	assert "%{_pe::3}%" contains "for 10 seconds" with "The potion effect should be for 10 seconds"
	assert "%{_pe::4}%" contains "without particles for 2 minutes without icon" with "The potion effect should have no icon/particles for 2 minutes"

	apply {_pe::*} to {_e} # Poor guy
	assert type of potion effects of {_e} contains darkness, night vision, nausea and slowness with "The poor sheep should have had these 4 effects added"

	# We'll miss ya lil buddy
	delete entity within {_e}
