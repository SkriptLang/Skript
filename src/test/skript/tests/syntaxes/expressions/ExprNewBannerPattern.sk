test "new banner pattern":
	set {_pattern} to a new creeper banner pattern colored red
	assert {_pattern} is a banner pattern type with "New banner pattern is not a banner pattern type"
	set {_item} to a banner
	add {_pattern} to the banner patterns of {_item}
	assert the banner patterns of {_item} contains {_pattern} with "Banner pattern not applied to item"
	remove {_pattern} from the banner patterns of {_item}
	assert the banner patterns of {_item} does not contain {_pattern} with "Banner pattern was not removed from item"
	broadcast "Setting 1st"
	set the 1st banner pattern of {_item} to {_pattern}
	assert the 1st banner pattern of {_item} is {_pattern} with "Banner pattern not applied to 1st banner pattern of item"
	broadcast "Clearing 1st"
	clear the 1st banner pattern of {_item}
	assert the 1st banner pattern of {_item} is not set with "1st banner pattern of item was not cleared"

	set block at test-location to a banner
	assert the block at test-location is a banner with "Banner block was not set"
	add {_pattern} to the banner patterns of (block at test-location)
	assert the banner patterns of (block at test-location) contains {_pattern} with "Banner pattern not applied to block"
	remove {_pattern} from the banner patterns of (block at test-location)
	assert the banner patterns of (block at test-location) does not contain {_pattern} with "Banner pattern was not removed from block"
	set the 1st banner pattern of (block at test-location) to {_pattern}
	assert the 1st banner pattern of (block at test-location) is {_pattern} with "Banner pattern not applied to 1st banner pattern of block"
	clear the 1st banner pattern of (block at test-location)
	assert the 1st banner pattern of (block at test-location) is not set with "1st banner pattern of item was not block"
	set block at test-location to air
