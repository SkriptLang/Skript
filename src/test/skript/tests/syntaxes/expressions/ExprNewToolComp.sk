test "new tool component" when running minecraft "1.20.6":
	###
		Combines:
			ExprNewToolComp
			ExprToolCompMiningSpeed
			ExprToolCompDamage
			ExprToolCompRules
			ExprToolComponent
	###

	set {_component} to a new tool component
	assert {_component} is a tool component with "New tool component is not a tool component"
	set the default mining speed of {_component} to 20
	assert the default mining speed of {_component} is 20 with "Mining speed of tool component did not get set"
	set the damage per block of {_component} to 10
	assert the damage per block of {_component} is 10 with "Damage per block of tool component did not get set"

	create a new tool rule and store it in {_rule}:
		set the tool rule blocks to oak log, stone and obsidian
	add {_rule} to the tool rules of {_component}
	assert the tool rules of {_component} contains {_rule} with "Tool rule was not added to variable component's tool rules"

	set {_item} to diamond pickaxe
	set the tool component of {_item} to {_component}
	assert the tool component of {_item} is {_component} with "Item's tool component does not match variable component"
	assert the default mining speed of {_item} is 20 with "Mining speed of item's tool component is not 20"
	assert the damage per block of {_item} is 10 with "Damage per block of item's tool component is not 20"
	assert the tool rules of {_item} contains {_rule} with "Tool rule was not added to item's tool rules"
	remove {_rule} from the tool rules of {_item}
	assert the tool rules of {_item} does not contain {_rule} with "Tool rule was not removed from item's tool rules"
