test "amount of objects":
	set {_objects::*} to (1 and 2)
	set {_amount} to amount of {_objects::*}
	assert {_amount} is 2 with "size of list was wrong - 1"

	set {_objects::*} to ("hello", "there" and 1)
	set {_amount} to amount of {_objects::*}
	assert {_amount} is 3 with "size of list was wrong - 2"

	assert size of (1, 2 and 3) is 3 with "size of literal list was wrong"
	assert size of (1, {_objects::*} and 3) is 5 with "size of mixed list was wrong"

test "amount of items":
	assert amount of (3 of stone) is 3 with "was wrong"
	set {_item} to 3 of stone
	assert amount of {_item} is 3 with "was wrong"

test "recursive size":
	set {_a} to 1
	set {_a::*} to 1, 2 and 3
	set {_a::1::*} to 1 and 2
	set {_a::1::1::1::1::1} to 5
	assert recursive size of {_a::*} is 6 with "recursive size was wrong"
