test "armour slot":
	spawn zombie at test-location:
		set boots of event-entity to golden boots
		assert boots of event-entity are golden boots with "Gold boots were not applied"
		set leggings of event-entity to iron leggings
		assert leggings of event-entity are iron leggings with "Iron leggings were not applied"
		set chestplate of event-entity to diamond chestplate
		assert chestplate of event-entity is diamond chestplate with "Diamond chestplate was not applied"
		set helmet of event-entity to dirt
		assert helmet of event-entity is dirt with "Dirt helmet was not applied"
		assert armour of event-entity contains dirt, diamond chestplate, iron leggings and golden boots with "Armour contents were not correct"
		clear armour of event-entity
		assert armour of event-entity does not contain dirt, diamond chestplate, iron leggings and golden boots with "Failed to clear EquipmentSlots"
		delete event-entity

test "body armor wolf" when running minecraft "1.20.5":
	spawn wolf at test-location:
		set {_entity} to event-entity

	set body armor of {_entity} to wolf armor
	assert body armor of {_entity} is wolf armor with "Body armor of entity is not wolf armor"
	clear body armor of {_entity}
	assert body armor of {_entity} is air with "Body armor of entity did not get cleared"
	clear entity within {_entity}

test "invalid body armor" when running minecraft "1.20.5":
	loop a zombie, a skeleton, a zombie horse and a skeleton horse:
		spawn loop-value at test-location:
			assert (body armor of event-entity) is not set with "Body armor of %type of event-entity% is accessible"
			set body armor of event-entity to diamond chestplate
			assert (body armor of event-entity) is not set with "Body armor of %type of event-entity% is accessible"
			clear event-entity

test "saddle slot entities" when running minecraft "1.21.5":
	loop a pig, a strider, a horse, a donkey, a mule, a camel, a llama, a zombie horse and a skeleton horse:
		spawn loop-value at test-location:
			set saddle slot of event-entity to a saddle
			assert saddle slot of event-entity is a saddle with "Saddle of %type of event-entity% was not set"
			clear saddle slot of event-entity
			assert saddle slot of event-entity is air with "Saddle of %type of event-entity% was not cleared"
			clear event-entity

test "invalid saddle entities" when running minecraft "1.21.5":
	loop a zombie, a skeleton, a cow and a sheep:
		spawn loop-value at test-location:
			assert (saddle slot of event-entity) is not set with "Saddle slot of %type of event-entity% is accessible"
			set saddle slot of event-entity to diamond chestplate
			assert (saddle slot of event-entity) is not set with "Saddle slot of %type of event-entity% is accessible"
			clear event-entity
