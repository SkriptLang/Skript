test "entities in chunk":
	spawn 10 sheep at test-location:
		add event-entity to {_sheep::*}
		add 1 to {_count}
	assert {_count} and size of entities within {_sheep::*} is 10 with "Did not count 10 on the entities within {_sheep::*}: %{_sheep::*}%"
	clear all entities

	set {_ground} to highest solid block at test-location
	set {_chunk} to chunk at {_ground}

	loop (integers between -2 and 2):
		spawn 2 sheep at {_ground} ~ vector(loop-value, 0, loop-value)

	set {_corner::1} to {_ground}
	set {_corner::2} to {_ground} ~ vector(8, 4, 8)
	loop (integers between 1 and 8):
		spawn 2 sheep at {_ground} ~ vector(loop-value, 0, loop-value)

	assert size of all entities in {_chunk} >= 10 with "Size of all entities in spawn chunk is not >= 10"
	assert size of all entities in radius 3 of {_ground} >= 10 with "Size of all entities in radius 3 of -24, -60, -24 is not >= 10"
	assert size of all entities within {_corner::1} and {_corner::2} >= 10 with "Size of all entities within cuboid is not >= 10"


	loop all entities in {_chunk}:
		add loop-entity to {_chunk::*}
	assert size of {_chunk::*} >= 10 with "Size of all entities in spawn chunk is not >= 10 (iterating)"

	loop all sheep in radius 3 of {_ground}:
		add loop-entity to {_radius::*}
	assert size of {_radius::*} >= 10 with "Size of all sheep in radius 3 of spawn is not >= 10 (iterating)"

	loop all sheep within {_corner::1} and {_corner::2}:
		add loop-entity to {_cuboid::*}
	assert size of {_cuboid::*} >= 10 with "Size of all sheep within cuboid is not >= 10 (iterating)"


	delete all entities in {_chunk}
	assert size of all entities in {_chunk} = 0 with "Size of all entities in spawn chunk != 0"

	delete all entities in radius 3 of {_ground}
	assert size of all entities in radius 3 of {_ground} = 0 with "Size of all entities in radius 3 of spawn != 0"

	delete all entities within {_corner::1} and {_corner::2}
	assert size of all entities within {_corner::1} and {_corner::2} = 0 with "Size of all entities in cuboid != 0"
  
	clear entities within {_e::*}
	assert entities within {_e::*} where [entity input is valid] is not set with "Not all the sheep were cleared"
	clear all entities
