test "replace effect":
	loop 8 times:
		set {_text::%loop-number%} to "My name is bob and this is my friend bob!"

	# Original Replace
	replace all "bob" in {_text::1} with "bobby"
	replace all "bob" with "bobby" in {_text::2}

	# Replace First
	replace first "bob" in {_text::3} with "bobby"
	replace first "bob" with "bobby" in {_text::4}

	# Case Sensitivity
	replace all "bob" in {_text::5} with "BOBBY" with case sensitivity
	replace all "BOB" with "bobby" in {_text::6} with case sensitivity
	replace first "bob" in {_text::7} with "BOBBY" with case sensitivity
	replace first "BOB" with "bobby" in {_text::8} with case sensitivity

	assert {_text::1} = "My name is bobby and this is my friend bobby!" with "replace all pattern 1 (case sensitivity disabled) failed"
	assert {_text::2} = "My name is bobby and this is my friend bobby!" with "replace all pattern 2 (case sensitivity disabled) failed"
	assert {_text::3} = "My name is bobby and this is my friend bob!" with "replace first pattern 1 (case sensitivity disabled) failed"
	assert {_text::4} = "My name is bobby and this is my friend bob!" with "replace first pattern 2 (case sensitivity disabled) failed"
	assert {_text::5} = "My name is bob and this is my friend bob!" with "replace all pattern 1 (case sensitivity enabled) failed"
	assert {_text::6} = "My name is BOBBY and this is my friend BOBBY!" with "replace all pattern 2 (case sensitivity enabled) failed"
	assert {_text::7} = "My name is bob and this is my friend bob!" with "replace first pattern 1 (case sensitivity enabled) failed"
	assert {_text::8} = "My name is BOBBY and this is my friend bob!" with "replace first pattern 2 (case sensitivity enabled) failed"

