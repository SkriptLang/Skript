#
# This script is a template for commands which have a cooldown.
# I will add a more intuitive way of handling cooldowns/countdowns in the future,
# But for now please use something like this.
# Make sure that you do not use delays for cooldowns as they stop when the server stops.
#

# This command allows each player to get infinite cakes
# but the command has a cooldown of one minute (per player)

# Cooldowns became much simpler in Skript 2.3.1

command /cake:
	description: Recieve a cake, but you can only do this once per minute!
	permission: cake.is_a_lie
	executable by: players
	cooldown: 1 minute
	cooldown message: You have to wait %remaining time% before you can use this command again!
	cooldown storage: {cake::%player's uuid%::lastused} # Stores cooldown in a variable that can be accessed
	trigger:
		player doesn't have space for a cake:
			message "You do not have enough space in your inventory to hold the cake!"
			cancel the cooldown
		else:
			send "The cake is a lie"
			give a cake to the player
