#
# A simple home script which allows players with the permission 'skript.home' to
# define a home location with /sethome and teleport to that location whenever they want to with /home.
#

command /sethome:
	description: Set your home
	permission: skript.home
	executable by: players
	trigger:
		set {home::%player's uuid%} to player's location
		message "Set your home to <grey>%location of player%<reset>"

command /home:
	description: Teleport yourself to your home
	permission: skript.home
	executable by: players
	trigger:
		if {home::%player's uuid%} is not set:
			message "You have not set your home yet!"
		else:
			teleport player to {home::%player's uuid%}
