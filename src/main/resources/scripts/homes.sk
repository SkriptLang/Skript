#
# A simple home script which allows players with the permission 'skript.home' to
# define a home location with /sethome and teleport to that location whenever they want to with /home.
#

command /sethome <text>:
	description: Sets a home to your location with the name you specify.
	permission: skript.home
	executable by: players
	trigger:
		if {homes.%player%::%arg-1%} is set:
			send "A home with that name is already set, please specify a new name."
		else:
			set {homes.%player%::%arg-1%} to location of player
			message "Set your home '%arg-1%' to %location of player%"

command /deletehome <text>:
	description: Deletes the home you specify if it exists.
	permission: skript.home
	aliases: delhome
	executable by: players
	trigger:
		if {homes.%player%::%arg-1%} is set:
			delete {homes.%player%::%arg-1%}
			send "Successfully deleted your home '%arg-1%'."
		else:
			send "You don't have a home with that name."

command /home <text>:
	description: Teleport yourself to your home.
	permission: skript.home
	executable by: players
	trigger:
		if {homes.%player%::%arg-1%} is set:
			teleport player to {homes.%player%::%arg-1%}
			send "Successfully teleported you to your home '%arg-1%'."
		else:
			send "You don't have a home with that name, here is a list of your homes."
			loop {homes.%player%::*}:
				send "- %loop-index%"