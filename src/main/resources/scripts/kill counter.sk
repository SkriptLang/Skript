#
# Counts kills per life, in total, and the highest kill streak.
#

death:
	attacker is a player
	attacker isn't the victim
	add 1 to {kill counter::%attacker's uuid%::kills}
	add 1 to {kill counter::%attacker's uuid%::streak}
	{kill counter::%attacker's uuid%::streak} > {kill counter::%attacker's uuid%::max}:
		set {kill counter::%attacker's uuid%::max} to {kill counter::%attacker's uuid%::streak}

death of player:
	clear {kill counter::%victim's uuid%::streak}

command /kills:
	executable by: players
	trigger:
		# You can use ? after a variable as a placeholder if it isn't set.
		message "You have killed %{kills counter::%player's uuid%::streak} ? ""no""% mobs and players in this life out of %{kill counter::%player's uuid%::kills} + 0% kills in total."
		message "Your maximum kill streak is %{kill counter::%player's uuid%::max} + 0% kills in one life."
